// Copyright (C) 2015-2016 dataFascia Corporation - All Rights Reserved
// For license information, please contact http://datafascia.com/contact
package com.datafascia.emerge.ucsf.harm;

import ca.uhn.fhir.model.dstu2.resource.Encounter;
import ca.uhn.fhir.model.dstu2.resource.Flag;
import com.datafascia.emerge.ucsf.harm.HarmEvidenceUpdater.Event;
import com.datafascia.emerge.ucsf.harm.HarmEvidenceUpdater.EventType;

global com.datafascia.emerge.ucsf.HarmEvidence harmEvidence;
global com.datafascia.emerge.ucsf.harm.AlignmentOfGoalsUpdater alignmentOfGoalsUpdater;

rule "Initialize alignment of goals data with default values"
when
  Event( type == EventType.ADMIT_PATIENT )
  $encounter: Encounter()
then
  alignmentOfGoalsUpdater.updateAdvanceDirective(harmEvidence, $encounter);
  alignmentOfGoalsUpdater.updatePatientCareConferenceNote(harmEvidence, $encounter);
  alignmentOfGoalsUpdater.updatePhysicianOrdersForLifeSustainingTreatment(harmEvidence, $encounter);
  alignmentOfGoalsUpdater.updateCodeStatus(harmEvidence, $encounter);
end

rule "Update advance directive"
when
  Event( type == EventType.UPDATE_FLAG )
  $flag: Flag( code.codingFirstRep.code == "AD" )
  $encounter: Encounter()
then
  alignmentOfGoalsUpdater.updateAdvanceDirective(harmEvidence, $encounter);
end

rule "Update patient care conference note"
when
  Event( type == EventType.UPDATE_FLAG )
  $flag: Flag( code.codingFirstRep.code == "PATIENT_CARE_CONFERENCE_NOTE" )
  $encounter: Encounter()
then
  alignmentOfGoalsUpdater.updatePatientCareConferenceNote(harmEvidence, $encounter);
end

rule "Update physician orders for life sustaining treatment"
when
  Event( type == EventType.UPDATE_FLAG )
  $flag: Flag( code.codingFirstRep.code == "POLST" )
  $encounter: Encounter()
then
  alignmentOfGoalsUpdater.updatePhysicianOrdersForLifeSustainingTreatment(harmEvidence, $encounter);
end

rule "Update code status"
when
  Event( type == EventType.TIMER )
  $encounter: Encounter()
then
  alignmentOfGoalsUpdater.updateCodeStatus(harmEvidence, $encounter);
end
